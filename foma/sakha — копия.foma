###sakha.foma###

define BackVNonLab [ а | а а | ы | ы ы | ы а ];
define FrontVNonLab [ э | э э | и | и и | и э ];
define FrontHighVLab [ ү | ү ү | ү ө ];
define FrontLowVLab [ ө | ө ө ];
define BackHighVLab [ у | у у | у о ];
define BackLowVLab [ о | о о ];
define Vowel [ а | а а | ы | ы ы | ы а | э | э э | и | и и | и э | ү | ү ү | ү ө | ө | ө ө | у | у у | у о | о | о о ];
define Consonant [ б | в | г | ҕ | д | д ь | ж | з | й | к | л | м | н | ҥ | н ь | п | р | с | һ | т | ф | х | ц | ч | ш | щ | ъ | ь ];

# affix_specific

define CaseAcc ы -> н ы || Vowel "^" _ "^" ;

define CaseAbl т а н -> т т а н || Vowel "^" _ "^" ;

define CaseIns н а н -> ы н а н || Consonant "^" _ "^" ;

# harmony

define NonLabFront а -> э , ы -> и , ы а -> и э || FrontVNonLab Consonant* "^" [? - "^"]* _  ;

define NonLabBack э -> а , и -> ы , и э -> ы а || BackVNonLab Consonant* "^" [? - "^"]* _  ;

define LabFrontHighFromBack а -> э , ы -> ү , ы а -> ү ө || FrontHighVLab Consonant* "^" [? - "^"]* _  ;

define LabFrontHighFromFront э -> э , и -> ү , и э -> ү ө || FrontHighVLab Consonant* "^" [? - "^"]* _  ;

define LabFrontLowFromBack а -> ө , ы -> ү , ы а -> ү ө || FrontLowVLab Consonant* "^" [? - "^"]* _  ;

define LabFrontLowFromFront э -> ө , и -> ү , и э -> ү ө || FrontLowVLab Consonant* "^" [? - "^"]* _  ;

define LabBackHighFromBack а -> а , ы -> у , ы а -> у о || BackHighVLab Consonant* "^" [? - "^"]* _  ;

define LabBackHighFromFront э -> а , и -> у , и э -> у о || BackHighVLab Consonant* "^" [? - "^"]* _  ;

define LabBackLowFromBack а -> о , ы -> у , ы а -> у о || BackLowVLab Consonant* "^" [? - "^"]* _  ;

define LabBackLowFromFront э -> о , и -> у , и э -> у о || BackLowVLab Consonant* "^" [? - "^"]* _  ;

# consonants

define Voicing к -> г , п -> б , с -> һ , х -> ҕ ||  _ "^" Vowel ;

define Devoicing г -> к , б -> п , һ -> с , ҕ -> х || [к | п | с | х] "^" _  ;

define NasalCH ч -> н ь || [ҥ | м | н] "^" _  ;

define VoicedCH ч -> д ь || й _  ;

define NasalB б -> м || ҥ "^" _  ;

define NasalBAssimN н   " ^ "   б -> м   " ^ "   м ;

define DevoicedBAssimN т   " ^ "   б -> п   " ^ "   п ;

define NasalL л -> н || [ҥ | м | н] "^" _  ;

define DevoicedGHColloquial ҕ -> х || й _  ;

define PlosiveGH ҕ -> г || [ы | у | и | ү | й | л | р] "^" _  ;

define NasalGH ҕ -> ҥ || н "^" _  ;

define NasalGHAssimN н   " ^ "   ҕ -> ҥ   " ^ "   ҥ ;

define PlosiveGHAssimT т   " ^ "   ҕ -> к   " ^ "   к ;

define DevoicedGH ҕ -> х || х "^" _  ;

define DevoicedB б -> п || [т | к | п] "^" _  ;

define DevoicedL л -> т || [к | п | т] "^" _  ;

define AssimilatedGHdevoiced ҕ -> к || [к | п | т] "^" _  ;

define DeleteStemPart т -> 0 , п -> 0 , к -> 0 || л _ п , м _ к , ҥ _ п , ҥ _ т ;

define AssimilationCHIncosistent с ч -> ч ч , т ч -> ч ч ;

# 

# loan_adaptation


read lexc sakha.lexc
define Lexicon;


read lexc sakha_gloss.lexc
define LexiconGloss;

define Phonetic NonLabFront .o.
NonLabBack .o.
LabFrontHighFromBack .o.
LabFrontHighFromFront .o.
LabFrontLowFromBack .o.
LabFrontLowFromFront .o.
LabBackHighFromBack .o.
LabBackHighFromFront .o.
LabBackLowFromBack .o.
LabBackLowFromFront .o.
Voicing .o.
Devoicing .o.
NasalCH .o.
VoicedCH .o.
NasalB .o.
NasalBAssimN .o.
DevoicedBAssimN .o.
NasalL .o.
DevoicedGHColloquial .o.
PlosiveGH .o.
NasalGH .o.
NasalGHAssimN .o.
PlosiveGHAssimT .o.
DevoicedGH .o.
DevoicedB .o.
DevoicedL .o.
AssimilatedGHdevoiced .o.
DeleteStemPart .o.
AssimilationCHIncosistent;

define Grammar LexiconGloss .o.
CaseAcc .o.
CaseAbl .o.
CaseIns .o.
Phonetic^5 .o.
#Phonetic .o.
#Phonetic .o.
#Phonetic .o.
#Phonetic .o.
Phonetic;

define GrammarClean Lexicon .o.
CaseAcc .o.
CaseAbl .o.
CaseIns .o.
Phonetic^5 .o.
#Phonetic .o.
#Phonetic .o.
#Phonetic .o.
#Phonetic .o.
Phonetic;

regex Grammar;
